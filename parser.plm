require "library/char"

type String = list<char>
type ParserResult<A> = (Result<A, str>, String)
type Parser<A> { Parser(fn(String): ParserResult<A>) }

fn run_parser<A>(parser: Parser<A>, inp: String): ParserResult<A> {
  return switch parser {
    case Parser(f) => f(inp)
  }
}

fn parse<A>(parser: Parser<A>, inp: String): Option<A> => switch run_parser(parser, inp) {
  case (Ok(value), ?) => Some(value)
  case (?, ?) => None
}

extend traversable<Parser> {
  fn map<A, B>(self: Parser<A>, f: fn(A): B): Parser<B> => 
    Parser(fn (input) => switch self {
      case Parser(parser) => switch parser(input) {
        case (result, rest) => (result.map(f), rest)
      }
    })
}

extend monadic<Parser> {
  fn bind<A, B>(self: Parser<A>, f: fn(A): Parser<B>): Parser<B> => 
    Parser(fn (input) => switch self.run_parser(input) {
      case (Ok(value), rest) => f(value).run_parser(rest)
      case (Error(err), rest) => (Error(err), rest)
    })
  
  fn pure<A>(value: A): Parser<A> => 
    Parser(fn (input) => (Ok(value), input))
}

fn satisfy(predicate: fn(char): bool): Parser<char> => 
  Parser(fn (inp) {
    return switch inp {
      case [] => (Error("Unexpected end of input"), [])
      case [c, ..rest] => {
        if predicate(c) {
          return (Ok(c), rest)
        } else {
          return (Error("Unexpected character $c"), inp)
        }
      }
    }
  }) 

infixl 6 <|>

fn (<|>)<A>(x: Parser<A>, y: Parser<A>): Parser<A> => 
  Parser(fn (input) => switch run_parser(x, input) {
    case (Ok(value), rest) => (Ok(value), rest)
    case (Error(_), _) => run_parser(y, input)
  })

fn many<A extends equality>(p: Parser<A>): Parser<[A]> =>
  (p.bind(fn (x) => 
    p.many().map(fn(xs) => [x] + xs)
  )) <|> pure([])

fn many1<A extends equality>(p: Parser<A>): Parser<[A]> =>
  p.bind(fn (x) => many(p).map(fn (xs) => [x] + xs))

fn string(s: String): Parser<String> =>
  switch s {
    case [] => pure([])
    case [c, ..cs] => 
      satisfy(fn (x) => x == c).bind(fn (_) => string(cs).map(fn (xs) => [c] + xs))
  }
  
fn character(c: char): Parser<char> => satisfy(fn (x) => x == c)

space: Parser<char> = satisfy(fn (c) => c == ' ')
space1: Parser<[char]> = many1(space)
spaces: Parser<[char]> = many(space)

newline: Parser<char> = satisfy(fn (c) => c == '\n' or c == '\r')

digit: Parser<char> = satisfy(is_digit)
digits: Parser<String> = many1(digit)

identifier: Parser<String> = 
  satisfy(is_alphabetic).bind(fn(c) =>
    many(satisfy(is_alphanumeric)).map(fn(cs) => [c] + cs)
  )

fn lex<A>(p: Parser<A>): Parser<A> => 
  spaces >> p << spaces

fn sepBy<A extends equality, B>(p: Parser<A>, sep: Parser<B>): Parser<[A]> =>
  p.bind(fn(x) => 
    many(sep.and_then(p)).map(fn(xs) => [x] + xs)
  ) <|> pure([])

fn sepBy1<A extends equality, B>(p: Parser<A>, sep: Parser<B>): Parser<[A]> =>
  p.bind(fn(x) => 
    many(sep.and_then(p)).map(fn(xs) => [x] + xs)
  )

fn manyTill<A extends equality, B>(p: Parser<A>, end: Parser<B>): Parser<[A]> =>
  end.bind(fn (_) => pure([])) <|> p.bind(fn(x) => 
    manyTill(p, end).map(fn(xs) => [x] + xs)
  )

anything = satisfy(fn (_) => true)

fn try<A>(p: Parser<A>): Parser<A> => 
  Parser(fn (input) => switch run_parser(p, input) {
    case (Ok(value), rest) => (Ok(value), rest)
    case (Error(err), _) => (Error(err), input)
  })

fn none_of(chars: String): Parser<char> => 
  satisfy(fn (c) => not (chars.elem(c)))

extend<A extends monoidal> boolean_algebra<Parser<A>> {
  fn not(self: Parser<A>): Parser<A> => 
    Parser(fn (input) => switch run_parser(self, input) {
      case (Ok(_), _) => (Error("Unexpected input"), input)
      case (Error(_), _) => (Ok(empty()), input)
    })

  fn and(self: Parser<A>, other: Parser<A>): Parser<A> =>
    self.bind(fn (_) => other)
  
  fn or(self: Parser<A>, other: Parser<A>): Parser<A> =>
    self <|> other
}

fn optional<A>(p: Parser<A>): Parser<Option<A>> => 
  p.map(Some) <|> pure(None)